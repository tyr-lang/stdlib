/**
 * An array that knows its size and type.
 *
 * @author Timm Felden
 */
public class Array[T : Type[any]] <: Seq[T] {
  public val length : size_t;
  private var data : FlatArray[T];

  public new (size : index_t) {
    length = size.unpack
    data = new FlatArray[T](length)
  }

  public new (size : index_t, init : Block[T]) {
    length = size.unpack
    data = new FlatArray[T](length)

    var i = 0U
    while(i < length) {
      data(i) = init.eval()
      ++i
    }
  }

  delete {
    delete data;
  }

  public def iterator : FlatArrayIterator[T] = new FlatArrayIterator[T](data, length)

  public def apply(index : index_t) : T = data(index);
  public def update(index : index_t, v : T) : T = data.update(index, v);

  public def isDefined(index : index_t) : bool = 0U <= index.unpack && index.unpack < length

  public def size : size_t = length

  public def view (offset : index_t = 0.implicit())
                  = ArrayView[T].from(data.shift(offset))

  // /**
  //  * Perform action for each remaining element of the array. Allow the block to
  //  * change the elements.
  //  */
  // public def foreach (x : BlockParameter[Ref[T]], action : Block[any]) : void {
  //   var i = 0U
  //   while(i < length) {
  //     x = data(i)
  //     ++i
  //     action.eval()
  //   }
  // }
}