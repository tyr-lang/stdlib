/**
 * The type of a class.
 *
 * @note we use Schubert et. al. 83 as a type checking algorithm because it has
 *       unparalleled performance and the current approach requires static
 *       linking anyway. This also means, that we will change that as soon as we
 *       start to implement a dynamic linking approach.
 *       (note to self: my initial SKilL next pointer algorithm was sufficient)
 *
 * @note Implementiations of classes are actually anonymous sub types of Class
 *       whose class is the Class of classes.
 *
 * @note it is not possible for this class to have (object) def or class members
 *
 * @note the signature of this type cannot be changed without changing the
 *   compiler
 *
 * @todo 0.8 should be sealed
 *
 * @author Timm Felden
 */
public
class Class[+T : Class[Object] := Object] <: Type[T], mem.eternal {

  /**
   * Class initialization is performed by the back-end globally.
   * The initialization of Class values happens even before evaluation of global
   * initialization.
   */
  private new <: reflection.native;

  /**
   * The first Type ID associated with this type.
   */
  val minTID : int;

  /**
   * The last Type ID associated with any subtype of this type. If this type
   * has no subtype then maxTID == minTid.
   *
   * @inv minTID <= maxTID
   *
   * @note for any subtype S of T: T.minTID < S.minTID && S.maxTID <= T.maxTID.
   * @note TIDs are dense, i.e. a type T has exactly maxTID - minTID - 1
   *       transitive subtypes
   */
  val maxTID : int;
  
  /**
   * This is a pointer to a list of implemented interfaces.
   * It is null if no interface is implemented.
   * The ITypes structure is provided by sleipnir and may be unified.
   */
  val itypes : mem.HeapPointer[ITypes];
  
  /**
   * This is a pointer to an itable. It is null if no interface is implemented.
   * We silently assume all interface member calls to be correct. Hence, there
   * is no need to add any checks here.
   */
  private val itable : mem.HeapPointer[ITable];

  /**
   * Check if this is a subtype or equal to super
   *
   * @note that must not be null
   *
   * @note we have to check both borders. Otherwise, we would accept unrelated
   *       types
   *
   * @note the class order is partial, i.e. it is possible that:
   *       (! T <= U && ! U <= T) == true
   *       An example of this is String and Type.
   */
  public type def <= (this : Class[Object], super : Class[Object]) : bool
                     <: operator.precedence[90] =
    (super.minTID <= this.minTID) &! this.maxTID <= super.maxTID

  /**
   * Check if this is a subtype and not equal to super
   *
   * @note that must not be null
   *
   * @note we have to check both borders. Otherwise, we would accept unrelated
   *       types
   */
  public type def < (this : Class[Object], super : Class[Object]) : bool
                    <: operator.precedence[90] =
    (super.minTID < this.minTID) &! this.maxTID <= super.maxTID

  /**
   * Check if ref is an instance of this type
   *
   * @note true if null is passed
   */
  public type def ? (this : Class[Object], ref : Object) : bool
                    <: operator.precedence[90] =
    null == ref || {
      val cls = ref.class
      ((this.minTID <= cls.minTID) &! cls.maxTID <= this.maxTID)
    }

  /**
   * Check if ref is an instance of this type and return it in that case
   *
   * @return null if this is not an instance of the class or if ref is null
   *
   * @note a future version may throw an exception if ref is no instance of this
   *
   * @note a future version may add a flag to stop program execution if ref is
   *       not an instance of this
   */
  public type def ! (this : Class[Object], ref : Object) : this
                    <: operator.precedence[90] =
    if (null == ref) null
    else if (this ? ref) this !! ref
    else null
}
