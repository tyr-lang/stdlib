with container.ArrayBuffer;
with container.index_t;
with container.size_t;
with container.UTF8Iterator;
with container.iterator.SingleElementIterator;


/**
 * A string buffer that acts as a String and can be modified. Concatenations
 * are efficienet. Any additions to the buffer are copies.
 */
public class StringBuffer <: String, ProvidesSolidStringView {

  val data : ArrayBuffer[byte];

  /**
   * Wrap an ArrayBuffer of bytes in a StringBuffer that takes ownership of it.
   */
  public
  type def wrapAndConsume (buffer : ArrayBuffer[byte]) : StringBuffer
                          = new StringBuffer(buffer);

  private
  new (data := data : ArrayBuffer[byte]);

  public
  new (from : String) {
    data = new ArrayBuffer[byte](from.size())
    from.bytes().foreach c do (data += c)
  }

  public
  new (sizeHint : index_t = 16.implicit()) {
    data = new ArrayBuffer[byte](sizeHint)
  }

  public delete {
    delete data
  }

  override def bytes = data.iterator()
  override def size : size_t = data.size()

  public def apply (index : index_t) : byte = data(index.unpack)

  override def implicit : SolidStringView = new SolidStringView(
    data := mem.HeapPointer[byte] !! data.view(),
    length := data.size()
  )

  public def += (c : byte) : void <: operator.precedence[20] = data += c

  public def += (other : String) : void <: operator.precedence[20] {
    data.ensureCapacity(data.size() + other.size())
    other.bytes().foreach c do (data += c)
  }

  public def receive() = new StringReceiverProxy(this)
  public
  def :: (ref : StringReceiverTarget) <: operator.postfix = receive() + ref

  public def clear : void = data.clear()

  test "use" {
    val buff = new StringBuffer
    buff += "hello"
    // " "
    buff += 0x20i8
    buff += "world"
    buff += "!"

    val expected = "hello world!"

    val r = expected.size() == buff.size() &&
            expected.bytes().forall c,i do (c == buff(i))

    delete buff
    r
  }
}

class StringReceiverProxy <: container.StringReceiver {
  private val base : StringBuffer;
  new (base := base : StringBuffer);

  override
  def hintBytes(size : index_t) : void {
    val b = base.data
    b.ensureCapacity(b.size() + size.unpack)
  }

  override
  def put(c : character) : void {
    new UTF8Iterator(new SingleElementIterator[character](c)).foreach b do {
      base += b
    }
  }

  override
  def put(str : SolidStringView) : void {
    val d = base.data
    val s = d.size()
    d.ensureCapacity(s + str.size)
    mem.raw.copy(base.data.view(s), str.data, str.size)
  }
}