with container.FlatArray;

/**
 * char const* with a \0 terminator
 *
 * @note When constructing CStrings from Tyr Strings, one must be aware that Tyr
 *       Strings may contain \0 characters anywhere. For now, the behaviour of
 *       converting such a string is undefined. A future version of Tyr might
 *       offer an optional check for this issue.
 */
public type CString <: FlatArray[byte] {

  /**
   * Create from a Tyr String.
   */
  public new (from : String) {
    FlatArray[byte].new(from.size()+1U);

    var i = 0
    from.bytes().foreach c do {
      this(i) = c;
      ++i
    }
    this(i) = 0i8
  }

  /** release the string's memory */
  public delete {
    FlatArray[byte].delete;
    this = null
  }

  /** print the string to stdout */
  public type def print (this : CString) : int extern c.puts;


  test "hello" {
    var s = new CString("hello\n");
    s.print()
    delete s
    null == s
  }
}
